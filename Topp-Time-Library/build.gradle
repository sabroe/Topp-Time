plugins {
    id 'java-library-distribution'
    id 'maven-publish'
    id 'signing'
    id 'groovy'
}

//group 'io.github.sabroe.topp'
//version '0.9.0-test'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    useJUnitPlatform()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    api 'org.slf4j:slf4j-api:1.7.32'
    testImplementation 'org.slf4j:slf4j-simple:1.7.32'

//    implementation 'org.threeten:threeten-extra:1.7.0'

    testImplementation 'org.codehaus.groovy:groovy:3.0.9'
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'topp-time-utility'
            version = project.version
            from components.java
            pom {
                name = 'Topp-Time'
                description = 'Topp Time is an open-source Java library implementing timeline manipulation.'
                url = 'https://github.com/sabroe/Topp-Time'
                inceptionYear = '2021'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/sabroe/Topp-Time/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sabroe'
                        name = 'Morten Sabroe Mortensen'
                        email = 'morten.sabroe.mortensen@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com:sabroe/Topp-Time.git'
                    developerConnection = 'scm:git:ssh://github.com:sabroe/Topp-Time.git'
                    url = 'https://github.com/sabroe/Topp-Time'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties['ossrhUsername']
                password = project.properties['ossrhPassword']
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
